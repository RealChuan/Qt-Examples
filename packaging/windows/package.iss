; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "MyApp"
#define MyAppVersion "0.0.1"
#define MyAppPublisher "Youth Co.,Ld."
#define MyAppURL "https://github.com/RealChuan"
#define MyAppExeName "MyApp.exe"
#define MyAppAssocName MyAppName + ""
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt
#define MyAppUninstallName "Uninstall"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2B7CC760-21EC-4184-890E-2E1766C6CE04}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
ChangesAssociations=yes
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=C:myapp\releases
OutputBaseFilename=MyApp_Installation_Package
SetupIconFile=C:\myapp\app.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
MinVersion=6.1.7600
DefaultGroupName={#MyAppName}
UninstallDisplayIcon=C:\myapp\app.ico
Uninstallable=yes
UninstallDisplayName={#MyAppName}
VersionInfoCompany={#MyAppPublisher}
VersionInfoCopyright={#MyAppPublisher}
VersionInfoDescription={#MyAppName}
VersionInfoOriginalFileName={#MyAppName}
VersionInfoProductTextVersion={#MyAppVersion}
VersionInfoProductVersion={#MyAppVersion}
VersionInfoTextVersion={#MyAppVersion}
SignTool = sha256
SignedUninstaller = yes

[Languages]
Name: "Chinese"; MessagesFile: "compiler:Languages\Chinese.isl"
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: checkablealone

[Files]
Source: "..\packet\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion signonce
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "..\packet\D3Dcompiler_47.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\opengl32sw.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Gui.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Network.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Pdf.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Svg.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Widgets.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\generic\*"; DestDir: "{app}\generic"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\iconengines\*"; DestDir: "{app}\iconengines"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\imageformats\*"; DestDir: "{app}\imageformats"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\networkinformation\*"; DestDir: "{app}\networkinformation"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\platforms\*"; DestDir: "{app}\platforms"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\styles\*"; DestDir: "{app}\styles"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\tls\*"; DestDir: "{app}\tls"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\translations\*"; DestDir: "{app}\translations"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\msvcr100.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Program Files (x86)\Microsoft Visual Studio\2019\Community\VC\Redist\MSVC\14.29.30133\x86\Microsoft.VC142.CRT\*"; DestDir: "{app}"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "C:\Program Files (x86)\Windows Kits\10\Redist\10.0.19041.0\ucrt\DLLs\x86\*"; DestDir: "{app}"; Flags: ignoreversion createallsubdirs recursesubdirs

[Registry]
Root: "HKA"; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; Flags: uninsdeletevalue
Root: "HKA"; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: "HKA"; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueData: "{app}\{#MyAppExeName},0"
Root: "HKA"; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: "HKA"; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"

Root: "HKCU"; Subkey: "SOFTWARE\Classes\CLSID\{{2B7CC760-21EC-4184-890E-2E1766C6CE04}"; ValueType: string; ValueData: "{#MyAppName}"; Flags: uninsdeletekey
Root: "HKCU"; Subkey: "SOFTWARE\Classes\CLSID\{{2B7CC760-21EC-4184-890E-2E1766C6CE04}\DefaultIcon"; ValueType: string; ValueData: """{app}\{#MyAppExeName}"""; Flags: uninsdeletekey
Root: "HKCU"; Subkey: "SOFTWARE\Classes\CLSID\{{2B7CC760-21EC-4184-890E-2E1766C6CE04}\shell\Open\Command"; ValueType: string; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Flags: uninsdeletekey
Root: "HKCU"; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\MyComputer\NameSpace\{{2B7CC760-21EC-4184-890E-2E1766C6CE04}"; ValueType: string; ValueData: "{#MyAppName}"; Flags: uninsdeletekey

Root: "HKCU"; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "{#MyAppName}"; ValueData: """{app}\{#MyAppExeName}"""; Flags: uninsdeletevalue

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{group}\{#MyAppUninstallName}"; Filename: "{uninstallexe}"

[Run]
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""MyApp"" program=""{app}\{#MyAppExeName}"" "; Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""MyApp"" program=""{app}\{#MyAppExeName}"" dir=in action=allow enable=yes"; Flags: runhidden;
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: filesandordirs; Name: "{localappdata}\MyApp"
Type: filesandordirs; Name: "{localappdata}\Youth Co.,Ld\MyApp"

[Code]
procedure ExitProcess(exitCode:integer);
  external 'ExitProcess@kernel32.dll stdcall';

function KDetectSoft(strExeName: String): Boolean;
var ErrorCode: Integer;
var bRes: Boolean;
var strFileContent: AnsiString;
var strTmpPath: String;
var strTmpFile: String;
var strCmdFind: String;
var strCmdKill: String;
begin
  strTmpPath := GetTempDir();
  strTmpFile := Format('%sfindSoftRes.txt', [strTmpPath]);
  strCmdFind := Format('/c tasklist /nh|find /c /i "%s" > "%s"', [strExeName, strTmpFile]);
  strCmdKill := Format('/c taskkill /f /t /im %s', [strExeName]);
  bRes := ShellExec('open', ExpandConstant('{cmd}'), strCmdFind, '', SW_HIDE, ewWaitUntilTerminated, ErrorCode);
  if bRes then begin
      bRes := LoadStringFromFile(strTmpFile, strFileContent);
      strFileContent := Trim(strFileContent);
      if bRes then begin
         if StrToInt(strFileContent) > 0 then begin
            ShellExec('open', ExpandConstant('{cmd}'), strCmdKill, '', SW_HIDE, ewNoWait, ErrorCode);;
            Result:= true;
         end else begin
            Result:= true;
            Exit;
         end;
      end;
  end;
  Result :=true;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if 1=CurPageID then begin
      Result := KDetectSoft('MyApp.exe');
      Exit;
  end; 
  Result:= true;
end;

function InitializeUninstall(): Boolean;
begin
  Result := KDetectSoft('MyApp.exe');
end;
